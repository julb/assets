plugins {
	id 'me.julb.gradleplugins.semanticversioning'
	id 'com.github.ben-manes.versions'
}

allprojects {
	apply plugin: 'eclipse'
	apply plugin: 'maven-publish'
	
	eclipse {
	    classpath {
	        downloadSources = true
            downloadJavadoc = true
	    }
	    project {
            natures 'org.eclipse.buildship.core.gradleprojectnature'
        }
	}
		
	repositories {
		mavenLocal()
	 	mavenCentral()
	 	maven {
            url 'https://repo.spring.io/milestone'
        }
	}
	
	publishing {
		repositories {
			mavenLocal()
		}
	}

	afterEvaluate {
		// patch vs code
		if(project.plugins.hasPlugin(JavaPlugin)) {
			sourceSets.main.java.srcDirs += "build/generated/sources/annotationProcessor/java/main"
			sourceSets.test.java.srcDirs += "build/generated/sources/annotationProcessor/java/test"
		}

		// patch to disable spring native from unit tests
		// https://github.com/spring-projects-experimental/spring-native/blob/main/spring-aot-gradle-plugin/src/main/java/org/springframework/aot/gradle/SpringAotGradlePlugin.java#L155-L166
		if (project.plugins.hasPlugin('org.springframework.experimental.aot')) {
			generateTestAot.onlyIf { false }
			test.configure { t ->
				t.systemProperties.remove('spring.test.context.default.CacheAwareContextLoaderDelegate')
			}
		}
	}
}

afterEvaluate {
    def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { keyword -> version.toUpperCase().contains(keyword) }
        def regex = /^[0-9,.v-]+(-r)?$/
        def yearHourRegex = /^[0-9]{8,8}[.][0-9]{6,6}$/
        return !stableKeyword && (!(version ==~ regex) || version ==~ yearHourRegex)
    }
    
    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            isNonStable(it.candidate.version)
        }
    }
}