plugins {
    id 'application'
    id 'io.freefair.lombok'
    id 'me.julb.gradleplugins.java17'
    id 'me.julb.gradleplugins.additionaljars'
    id 'org.springframework.boot'
    id 'com.github.johnrengelman.shadow'
    id 'org.springframework.experimental.aot'
    id 'me.julb.gradleplugins.spring-native-aws-lambda'
}

dependencies {
    implementation platform(project(':bom'))
    annotationProcessor platform(project(':bom'))
    
    implementation 'org.springframework.cloud:spring-cloud-starter-function-webflux'
    implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation project(':libraries:library-utility-identifier')
    implementation project(':libraries:library-dto-simple')
    
    testImplementation project(':libraries:library-utility-constants')
    testImplementation project(':spring-boot-starters:spring-boot-starter-test')
}

application {
    mainClass = 'me.julb.Application'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'me.julb.Application'
        )
    }
}

test {
    useJUnitPlatform()
    jvmArgs = ["-Dlogging.level.root=OFF", "-Dlogging.level.org.springframework=OFF"]
}

springBoot {
    buildInfo()
}

bootBuildImage {
    enabled = false
}

shadowJar {
    classifier = 'aws'
    dependencies {
        exclude(
            dependency("org.springframework.cloud:spring-cloud-function-webflux")
        )
    }
    // Required for Spring
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories']
        mergeStrategy = "append"
    }
}

assemble.dependsOn = [shadowJar]