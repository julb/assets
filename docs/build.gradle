plugins {
    id 'me.julb.gradleplugins.asciidoctor'
    id 'me.julb.gradleplugins.aggregatejavadoc'
    id 'me.julb.gradleplugins.aggregatespringdocopenapidocs'
}

// Aggregate the JavaDoc.
aggregateJavadoc {
    title = "${rootProject.name} ${rootProject.version} API"
}

// AsciiDoctor generation.
asciidoctor {
    options doctype: 'book'
    
    resources {
        from('src/docs/resources') into 'resources'
    }
}

// Prepare document.
task docs(type: Copy) {
    dependsOn 'asciidoctor', 'aggregateJavadoc', 'aggregateSpringDocOpenApiDocs'

    into("$buildDir/packaged-docs/$version")

    from("${asciidoctor.outputDir}") {
        exclude 'index-single.html'
        into 'reference/html'
    }

    from("${asciidoctor.outputDir}") {
        include 'index-single.html', 'resources/**'
        rename ('index-single.html', 'index.html')
        into 'reference/htmlsingle'
    }
	
    from (aggregateJavadoc.outputDir) {
        into 'api'
    }
    
    from (aggregateSpringDocOpenApiDocs.outputDir) {
        into 'oas3'
    }
}

// Zip all for artifact publication.
task docsZip(type: Zip) {
    dependsOn docs
    from docs.destinationDir
}

// For Artifact handling.
artifacts {
    archives docsZip
}
